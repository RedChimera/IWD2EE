SILENT
COPY ~%mod_folder%/usInitialModifications.txt~ ~override~

 //Files in this folder may be used in multiple components and shouldn't affect the game unless a component is installed that uses the file.
COPY ~%mod_folder%/initial~ ~override~

COPY ~%mod_folder%/ieex_override~ ~override~
COPY ~%mod_folder%/ieex_main_folder~ ~./~

 //Turning the "test" creatures into regular creatures because their irregular CRE format can crash WeiDU when patching all creatures.
COPY_EXISTING ~00gob.cre~ ~override/test_01.cre~ ~00gob.cre~ ~override/test_02.cre~ ~00gob.cre~ ~override/test_03.cre~ ~00gob.cre~ ~override/testocle.cre~

COPY_EXISTING ~attstyl.ids~ ~override/attstyle.ids~ //This is to prevent a bug where WeiDU looks for attstyle.ids while extending scripts but it's not there in IWD2

COPY_EXISTING ~action.ids~ ~override~ //Patching action.ids to include the SpellRES actions
	REPLACE_TEXTUALLY EXACT_MATCH ~31 Spell(O:Target*,I:Spell*Spell)~ ~31 Spell(O:Target*,I:Spell*Spell)
31 SpellRES(S:RES*,O:Target*)~
	UNLESS ~31 SpellRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~95 SpellPoint(P:Target*,I:Spell*Spell)~ ~95 SpellPoint(P:Target*,I:Spell*Spell)
95 SpellPointRES(S:RES*,P:Target*)~
	UNLESS ~95 SpellPointRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~113 ForceSpell(O:Target,I:Spell*Spell)~ ~113 ForceSpell(O:Target,I:Spell*Spell)
113 ForceSpellRES(S:RES*,O:Target*)~
	UNLESS ~113 ForceSpellRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~114 ForceSpellPoint(P:Target,I:Spell*Spell)~ ~114 ForceSpellPoint(P:Target,I:Spell*Spell)
114 ForceSpellPointRES(S:RES*,P:Target*)~
	UNLESS ~114 ForceSpellPointRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~181 ReallyForceSpell(O:Target,I:Spell*Spell)~ ~181 ReallyForceSpell(O:Target,I:Spell*Spell)
181 ReallyForceSpellRES(S:RES*,O:Target*)~
	UNLESS ~181 ReallyForceSpellRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~191 SpellNoDec(O:Target*,I:Spell*Spell)~ ~191 SpellNoDec(O:Target*,I:Spell*Spell)
191 SpellNoDecRES(S:RES*,O:Target*)~
	UNLESS ~191 SpellNoDecRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~192 SpellPointNoDec(P:Target*,I:Spell*Spell)~ ~192 SpellPointNoDec(P:Target*,I:Spell*Spell)
192 SpellPointNoDecRES(S:RES*,P:Target*)~
	UNLESS ~192 SpellPointNoDecRES~

COPY_EXISTING ~action.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~160 ApplySpell(O:Target,I:Spell*Spell)~ ~160 ApplySpell(O:Target,I:Spell*Spell)
160 ApplySpellRES(S:RES*,O:Target)~
	UNLESS ~160 ApplySpellRES~

COPY_EXISTING ~trigger.ids~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~0x4031 HaveSpell(I:Spell*Spell)~ ~0x4031 HaveSpell(I:Spell*Spell)
0x4031 HaveSpellRES(S:Spell*)~
	UNLESS ~0x4031 HaveSpellRES~

COPY_EXISTING_REGEXP ~50CVENl0\.bcs~ ~override~ ~52MOROH3\.bcs~ ~override~ ~61PCFLOR\.bcs~ ~override~ //Fixing a bug where permanent effects would be removed from PCs via certain scripts
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ClearSpriteEffects(~ ~ApplySpellRES("USDISPEL",~
	END

COPY_EXISTING_REGEXP ~52CC[1-8]P[1-6]\.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ClearSpriteEffects(~ ~ApplySpellRES("USCHSORD",~
	END
/*
COPY_EXISTING ~animate.ids~ ~override~
	REPLACE_TEXTUALLY ~0x1001 Wyvern, White (Big)~ ~0x1000 Wyvern (Big)
0x1001 Wyvern, White (Big)~
	REPLACE_TEXTUALLY ~0x1201 Dragon, Black (BG2)~ ~0x1200 Dragon, Red (BG2)
0x1201 Dragon, Black (BG2)
0x1202 Dragon, Silver (BG2)~

APPEND ~sounds.ini~
~
[MDR1] /Red Dragon/
att1=red03
att1frame=0
att2=red04
att2frame=0
att3=red05
att3frame=0
att4=
att4frame=
btlcry=red01,red02
btlcryframe=0,0
damage=red06
damageframe=0
death=red07
deathframe=
fidget=
fidgetframe=
selected=red01,red02
selectedframe=0,0
fall=
fallframe=

[MDR3] /Silver Dragon/
att1=silv03
att1frame=0
att2=silv04
att2frame=0
att3=silv05
att3frame=0
att4=
att4frame=
btlcry=silv01,silv02
btlcryframe=0,0
damage=silv06
damageframe=0
death=silv07
deathframe=
fidget=
fidgetframe=
selected=silv01,silv02
selectedframe=0,0
fall=
fallframe=
~
*/


//Making it so Lua scripts can recognize a spell's actual level for a spellcaster.

COPY_EXISTING ~IEex_WEIDU.lua~ ~override~
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~ex_listspll *=.*~ ~ex_listspll = {}~
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~ex_listdomn *=.*~ ~ex_listdomn = {}~

COPY_EXISTING ~LISTSPLL.2da~ ~override~
	COUNT_2DA_COLS numcolumns
	COUNT_2DA_ROWS numcolumns numrows
	FOR (i = 0; i < numrows; ++i) BEGIN
		READ_2DA_ENTRY i 1 numcolumns brd
		READ_2DA_ENTRY i 2 numcolumns clr
		READ_2DA_ENTRY i 3 numcolumns drd
		READ_2DA_ENTRY i 4 numcolumns pal
		READ_2DA_ENTRY i 5 numcolumns rgr
		READ_2DA_ENTRY i 6 numcolumns sor
		READ_2DA_ENTRY i 7 numcolumns wiz
		READ_2DA_ENTRY i 8 numcolumns spell_res_ref
		PATCH_IF (FILE_EXISTS_IN_GAME ~%spell_res_ref%.spl~) BEGIN
			INNER_ACTION BEGIN
				COPY_EXISTING ~IEex_WEIDU.lua~ ~override~
					REPLACE_TEXTUALLY CASE_INSENSITIVE ~ex_listspll = {~ ~ex_listspll = {["%spell_res_ref%"] = {%brd%, %clr%, %drd%, %pal%, %rgr%, %sor%, %wiz%}, ~
			END
		END
	END

COPY_EXISTING ~LISTDOMN.2da~ ~override~
	COUNT_2DA_COLS numcolumns
	COUNT_2DA_ROWS numcolumns numrows
	FOR (i = 0; i < numrows; ++i) BEGIN
		READ_2DA_ENTRY i 1 numcolumns cil
		READ_2DA_ENTRY i 2 numcolumns cla
		READ_2DA_ENTRY i 3 numcolumns cse
		READ_2DA_ENTRY i 4 numcolumns che
		READ_2DA_ENTRY i 5 numcolumns cog
		READ_2DA_ENTRY i 6 numcolumns cte
		READ_2DA_ENTRY i 7 numcolumns cba
		READ_2DA_ENTRY i 8 numcolumns cma
		READ_2DA_ENTRY i 9 numcolumns cta
		READ_2DA_ENTRY i (numcolumns - 1) numcolumns spell_res_ref
		PATCH_IF (FILE_EXISTS_IN_GAME ~%spell_res_ref%.spl~) BEGIN
			INNER_ACTION BEGIN
				COPY_EXISTING ~IEex_WEIDU.lua~ ~override~
					REPLACE_TEXTUALLY CASE_INSENSITIVE ~ex_listdomn = {~ ~ex_listdomn = {["%spell_res_ref%"] = {%cil%, %cla%, %cse%, %che%, %cog%, %cte%, %cba%, %cma%, %cta%}, ~
			END
		END
	END

VERBOSE

INCLUDE ~%mod_folder%/components/change_ai_script_fix.tpa~