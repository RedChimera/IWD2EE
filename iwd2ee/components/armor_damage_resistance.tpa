//NOTES
//
OUTER_SPRINT stringarmor @56032
OUTER_SPRINT stringshield @56033
OUTER_SPRINT stringgeneric @56034

COPY_EXISTING_REGEXP ~.*\.itm~ ~override~
//Armor and shields grant resistance to physical damage equal to half their armor/shield bonus, rounded down
	READ_SHORT 0x1c us_item_category
	PATCH_IF (us_item_category >= 60 AND us_item_category <= 68) OR (us_item_category = 2) OR (us_item_category = 6) BEGIN
		us_bonus = 0
		READ_LONG 0x6a us_offset
		READ_SHORT 0x70 us_num_effects
		FOR (i=0; i < us_num_effects; ++i) BEGIN
			READ_SHORT us_offset us_opcode
			READ_LONG (us_offset + 0x8) us_parameter2
			PATCH_IF (us_opcode = modifyac AND us_parameter2 = armorbonus) BEGIN
				READ_LONG (us_offset + 0x4) us_parameter1
				us_bonus = us_parameter1 / 2
				i = us_num_effects
			END
			us_offset += effectsize
		END
		PATCH_IF (us_bonus > 0) BEGIN
			READ_LONG 0x54 us_desc
			PATCH_IF (us_desc <= 0) BEGIN
				READ_LONG 0x50 us_desc
			END
			SPRINT stringdamageresistance @56031
			INNER_PATCH_SAVE stringdamageresistance ~%stringdamageresistance%~ BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~<EXARDAMR>~ ~%us_bonus%~
			END
			GET_STRREF us_desc description_old
			INNER_PATCH_SAVE description_new ~%description_old%~ BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~%stringgeneric%~ ~ulgiash~
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~\(%stringarmor%.*\)~ ~\1
%stringdamageresistance%~
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~ulgiash~ ~%stringgeneric%~
			END
			INNER_ACTION BEGIN
				STRING_SET_EVALUATE us_desc ~%description_new%~
			END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifyslashingresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifypiercingresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifybludgeoningresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifymissileresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
		END
	END ELSE PATCH_IF (us_item_category = 12 OR us_item_category = 41 OR us_item_category = 47 OR us_item_category = 49 OR us_item_category = 53) BEGIN
		us_bonus = 0
		READ_LONG 0x6a us_offset
		READ_SHORT 0x70 us_num_effects
		FOR (i=0; i < us_num_effects; ++i) BEGIN
			READ_SHORT us_offset us_opcode
			READ_LONG (us_offset + 0x8) us_parameter2
			PATCH_IF (us_opcode = modifyac AND us_parameter2 = shieldbonus) BEGIN
				READ_LONG (us_offset + 0x4) us_parameter1
				us_bonus = us_parameter1 / 2
				i = us_num_effects
			END
			us_offset += effectsize
		END
		PATCH_IF (us_bonus > 0) BEGIN
			READ_LONG 0x54 us_desc
			PATCH_IF (us_desc <= 0) BEGIN
				READ_LONG 0x50 us_desc
			END
			SPRINT stringdamageresistance @56031
			INNER_PATCH_SAVE stringdamageresistance ~%stringdamageresistance%~ BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~<EXARDAMR>~ ~%us_bonus%~
			END
			GET_STRREF us_desc description_old
			INNER_PATCH_SAVE description_new ~%description_old%~ BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE ~%stringshield%~ ~%stringshield%
%stringdamageresistance%~
			END
			INNER_ACTION BEGIN
				STRING_SET_EVALUATE us_desc ~%description_new%~
			END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifyslashingresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifypiercingresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifybludgeoningresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=modifymissileresistance target=1 timing=2 parameter1=us_bonus STR_VAR resource=~USARMDAR~ END
		END
	END
	UNLESS ~USARMDAR~
	BUT_ONLY_IF_IT_CHANGES