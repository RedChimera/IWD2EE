//NOTES
//COPY ~%mod_folder%/are/AR6103.are~ ~override~

OUTER_PATCH us_faster_horde_variables BEGIN
	regionsize=0xc4
	doorsize=0xc8
END

//INCLUDE ~%mod_folder%/components/us_functions.tpa~ //Functions that may be used in multiple components

ACTION_IF NOT MOD_IS_INSTALLED ~iwd2ee.tp2~ ~2~ BEGIN
	COPY_EXISTING ~SPPR716.spl~ ~override~ ~SPWI804.spl~ ~override~
		LPF CLONE_EFFECT INT_VAR silent=1 check_headers=1 check_globals=0 match_opcode=142 opcode=288 parameter2=129 END
END

ACTION_IF NOT MOD_IS_INSTALLED ~iwd2ee.tp2~ ~4~ BEGIN

	COPY_EXISTING ~00HFAMES.itm~ ~override~
		LPF CLONE_EFFECT INT_VAR silent=1 check_headers=0 check_globals=1 match_opcode=142 opcode=288 parameter2=129 END

	COPY ~%mod_folder%/itm/itm_item_revisions/61GENWP.itm~ ~override~
		READ_LONG 0x18 theitemflags
		theitemflags&=0xFFFF7FFE
		WRITE_LONG 0x18 theitemflags
		SAY UNIDENTIFIED_DESC @12316
		IF_EXISTS

	COPY ~%mod_folder%/itm/itm_item_revisions/61POTNSB.itm~ ~override~
		SAY IDENTIFIED_DESC @12588

	COPY_EXISTING ~61pbrewt.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE ~GiveItemCreate(\"61GenWP\",\"PoisonTable\",1,0,0)~ ~GiveItemCreate("61GenWP","PoisonTable",3,0,0)~
			REPLACE_TEXTUALLY CASE_INSENSITIVE ~Global(\"61ExitTo6104Open\",\"GLOBAL\",1)~ ~False()~
		END
END

COPY_EXISTING ~61pbridg.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~.IsAnimationID.*
~ ~
~
	END

COPY_EXISTING ~61pexrmp.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~Global(\"FountainCount\",\"MYAREA\",4)~ ~~
	END

COPY_EXISTING ~61pslime.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~SetGlobalTimer(\"SlimeDoorTimer\",\"MYAREA\",ONE_DAY)~ ~SetGlobalTimer("SlimeDoorTimer","MYAREA",60)~
	END

COPY_EXISTING ~61sseeye.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~\(.IsAnimationID\)~ ~!CheckSpellState(LastMarkedObject,MIND_BLANK)
\1~
	END

<<<<<<<< .../script.baf

IF
	NumTimesTalkedToGT(0)
	AttackedBy([PC],DEFAULT)
	See([PC],0)
	GlobalGT("61NumEfreetiKilled","GLOBAL",9)
	Global("CurrentForm","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("CurrentForm","LOCALS",1)
		SetGlobal("ChangeForm","LOCALS",1)
		DialogInterrupt(FALSE)
END

>>>>>>>>
EXTEND_TOP ~61thoram.bcs~ ~.../script.baf~

STRING_SET_EVALUATE 35435 @4006
STRING_SET_EVALUATE 35458 @4007